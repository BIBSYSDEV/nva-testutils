plugins {
    // Apply the java-library plugin to add support for Java Library
    id 'java-library'
    id 'jacoco'
    id 'checkstyle'
    id 'pmd'
    id "nebula.lint" version "16.9.0"
}

repositories {
    mavenCentral()
}

def jupiterVersion = "5.8.1"
def jacksonVersion = "2.13.0"
def awsSdkVersion = "1.12.85"
def awsSdk2Version = '2.17.57'
def mockitoVersion = '4.0.0'
def hamcrestVersion = '2.2'
def javaFaker = '1.0.2'
def apacheCommons= '3.12.0'
def commonsValidator = '1.7'
dependencies {

    implementation group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
    implementation group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.13'

    implementation group: 'com.amazonaws', name: 'aws-lambda-java-core', version: '1.2.1'
    implementation group: 'com.amazonaws', name: 'aws-java-sdk-sts', version: awsSdkVersion

    implementation(group: 'software.amazon.awssdk', name: 's3', version: awsSdk2Version)

    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: jacksonVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-parameter-names', version: jacksonVersion
    implementation group: 'org.hamcrest', name: 'hamcrest', version: hamcrestVersion

    implementation group: 'org.apache.commons', name: 'commons-lang3', version: apacheCommons
    implementation group: 'commons-validator', name: 'commons-validator', version: commonsValidator


    testImplementation group: 'com.github.javafaker', name: 'javafaker', version: javaFaker
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: jupiterVersion
    testImplementation group: 'org.mockito', name: 'mockito-core', version: mockitoVersion

}

test {
    // Use junit platform for unit tests
    useJUnitPlatform()
}

test {
    useJUnitPlatform()
    failFast = true
    testLogging {
        events('skipped', 'passed', 'failed')
    }
    Map<String, String> envVariables = new HashMap<>()
    envVariables.put("ALLOWED_ORIGIN", "*")
    environment(envVariables)
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

pmd {
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
    ruleSets = []
    ignoreFailures = true
}

checkstyle {
    configFile = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml').asFile()
    showViolations = true
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
        html.stylesheet rootProject.resources.text.fromFile('config/checkstyle/checkstyle-simple.xsl')
    }
}

// jacoco configuration
check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn(jacocoTestReport)

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 0.0
            }
        }
        rule {
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 0.0
            }
        }
    }
}

